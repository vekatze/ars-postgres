import {
  core.c-string {c-string, from-c-string, to-c-string},
  core.system {panic},
  core.text {format},
  this.foreign {CONNECTION_OK},
}

foreign {
  PQconnectdb(pointer): pointer,
  PQstatus(pointer): int,
  PQfinish(pointer): void,
  PQhost(pointer): pointer,
  PQport(pointer): pointer,
  PQdb(pointer): pointer,
  PQuser(pointer): pointer,
  PQpass(pointer): pointer,
  PQerrorMessage(pointer): pointer,
}

data conninfo {
| Conn-Info(
    host: text,
    port: text,
    dbname: text,
    user: text,
    password: text,
  )
}

data connection-error {
| Connection-Error(message: text)
}

nominal {
  connection: type,
  _connection: type,
  connect(c: conninfo): either(connection-error, connection),
}

inline _from-c-string-pointer(p: pointer): text {
  from-c-string(magic cast(pointer, &c-string, p))
}

resource _connection {
  function (ptr: int) {
    let ptr = magic cast(int, pointer, ptr) in
    let _ = magic external PQfinish(ptr) in
    Unit
  },
  function (ptr: int) {
    let ptr = magic cast(int, pointer, ptr) in
    let !host = _from-c-string-pointer(magic external PQhost(ptr)) in
    let !port = _from-c-string-pointer(magic external PQport(ptr)) in
    let !dbname = _from-c-string-pointer(magic external PQdb(ptr)) in
    let !user = _from-c-string-pointer(magic external PQuser(ptr)) in
    let password = _from-c-string-pointer(magic external PQpass(ptr)) in
    let new-conn = connect(Conn-Info of {host = !host, port = !port, dbname = !dbname, user = !user, password}) in
    match new-conn {
    | Right(c) =>
      magic cast(connection, int, c)
    | Left(_) =>
      let message =
        format(
          "failed to clone the following connection:\nhost={} port={} dbname={} user={} password=<hidden>\n",
          [!host, !port, !dbname, !user],
        )
      in
      let unreachable on message = panic(message) in
      let _ = message in
      unreachable
    }
  },
}

data connection {
| _Connection(_connection)
}

define connect(c: conninfo): either(connection-error, connection) {
  let Conn-Info of {host, port, dbname, user, password} = c in
  pin conn-text =
    format("host={} port={} dbname={} user={} password={}", [
      host,
      port,
      dbname,
      user,
      password,
    ])
  in
  let conn-text = to-c-string(conn-text) in
  let conn on conn-text = magic external PQconnectdb(magic cast(&c-string, pointer, conn-text)) in
  let _ = conn-text in
  let conn = magic cast(pointer, connection, conn) in
  let status: int on conn = magic external PQstatus(magic cast(&connection, pointer, conn)) in
  if eq-int(status, CONNECTION_OK) {
    Right(conn)
  } else {
    let e on conn =
      Connection-Error(_from-c-string-pointer(magic external PQerrorMessage(
        magic cast(&connection, pointer, conn),
      )))
    in
    let _ = conn in
    Left(e)
  }
}
